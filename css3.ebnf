syntax = property-def (<space> property-def)* <opt-space>?

property-def = non-terminal <'='> <opt-space> component

<non-terminal> = <opt-space> non-property <opt-space>
               | <opt-space> property     <opt-space>

<component> = component-single
            / component-multiplied
            / components

component-single = literal
                 / keyword-value
                 / non-property
                 / property
                 / brackets

component-multiplied = component-single multiplier

components = adjacent
           | double-amp
           | single-pipe
           | double-pipe

keyword-value = identifier
non-property  = <'<'> identifier <'>'>
property      = <"<'"> identifier <"'>">
brackets      = <'['> <opt-space> component <opt-space> <']'> bang?

double-amp  = (<opt-space> component <opt-space> <'&&'> <opt-space>)+ component
single-pipe = (<opt-space> component <opt-space> <'|'>  <opt-space>)+ component
double-pipe = (<opt-space> component <opt-space> <'||'> <opt-space>)+ component
adjacent    = (<opt-space> component ','? <space>)+ component

multiplier = question
           | asterisk
           | plus
           | braces
           | hash 
           | hash-braces

hash-braces = <hash> braces

question  = <'?'>
asterisk  = <'*'>
plus      = <'+'>
braces    = bracesA | bracesA-B | bracesA-
bracesA   = <'{'> digit+ <'}'>
bracesA-B = <'{'> digit+ <','> digit+ <'}'>
bracesA-  = <'{'> digit+ <',}'>
hash      = <'#'>
bang      = <'!'>

digit = #'[0-9]'
literal = #'[-a-zA-Z0-9\u0028\u0029,/]+'
        | <"'"> #'[-a-zA-Z0-9\u0028\u0029,/\u005B\u005D]' <"'">
<identifier> = #'[a-zA-Z][-a-zA-Z0-9]*(?:\u0028\u0029)?(?x) #identifier'
opt-space = #'\s*'
space = #'\s+'
