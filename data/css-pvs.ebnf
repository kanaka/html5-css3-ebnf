assignments = assignment (<space> assignment)* <opt-space>?
(*assignments = assignment (<delimeter> assignment)* <opt-space>? *)

assignment = non-terminal <'='> <opt-space> single-bar

<non-terminal> = <opt-space> non-property <opt-space>
               | <opt-space> property     <opt-space>


(* Operator precendence: mult, juxt, &&, ||, | *)

single-bar = double-bar (<space> <'|'> <space> <opt-space> double-bar)*

double-bar = double-amp (<space> <'||'> <space> <opt-space> double-amp)*

double-amp = juxtapose (<space> <'&&'> <space> <opt-space> juxtapose)*

juxtapose  = (<opt-space> component comma? <space>)* component

component = component-single
          | component-multiplied

component-single = literal
                 / func
                 / keyword-value
                 / non-property
                 / property
                 / brackets
                 / block

component-multiplied = component-single multiplier

keyword-value = identifier
non-property  = <'<'> identifier <'>'>
property      = <"<'"> identifier <"'>">
brackets      = <'['> <opt-space> single-bar <opt-space> <']'> bang?
block         = '{' <space> juxtapose <space> '}'
func          = identifier <'('> <opt-space> single-bar <opt-space> <')'>

multiplier = question
           | asterisk
           | plus
           | braces
           | hash 
           | hash-braces

hash-braces = <hash> braces

question  = <'?'>
asterisk  = <'*'>
plus      = <'+'>
braces    = bracesA
          | bracesA-B
          | bracesA-
bracesA   = <'{'> digit+ <'}'>
bracesA-B = <'{'> digit+ <','> digit+ <'}'>
bracesA-  = <'{'> digit+ <',}'>
hash      = <'#'>
bang      = <'!'>

digit = #'[0-9]'
comma = ','
delimeter = <opt-space> ';' <opt-space>
literal = #'[-a-zA-Z0-9,/:;%]+'
        | <"'"> #'[-a-zA-Z0-9\u0028\u0029,/\u005B\u005D\u002A\u002B\u003C\u003E\u003D]' <"'">
        | #'[\u0028\u0029]'
<identifier> = #'@?[-a-zA-Z][-a-zA-Z0-9]*(?:\u0028\u0029)?(?x) #identifier'
opt-space = #'\s*'
space = #'\s+'
