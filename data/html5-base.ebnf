(*
element = simple-tag <opt-space>
        | start-tag (element | content)* end-tag <opt-space>

simple-tag = void-tag
           | empty-tag
void-tag  = <'<'> void-tags (<space> attribute)* <opt-space> <'>'>
empty-tag = <'<'> name (<space> attribute)* <opt-space> <'/>'>

start-tag = <'<'> name (<space> attribute)* <opt-space> <'>'>
end-tag   = <'</'> name <opt-space> <'>'>
*)


content = content-char-data
        | reference
        | comment

attribute       = name <opt-space> '=' <opt-space> attribute-value
(*
attribute-value = <'"'> (#'[^<&"]*' | reference)* <'"'>
                | <"'"> (#"[^<&']*" | reference)* <"'">
*)
attribute-value = <'"'> (#'[A-Za-z0-9_<>/\\ .]*' | reference)* <'"'>
                | <"'"> (#"[A-Za-z0-9_<>/\\ .]*" | reference)* <"'">

(* char-data-generic = #'[^<&]*' !(#'[^<&]'* ']]>' #'[^<&]*' ) *)
char-data-generic = #'[^<&"]*'
content-char-data-generic = #'[^<&]*'
(*
char-data = #'[A-Za-z0-9_<>/\\ .]*'
*)
(* Just include the Ahem glyphs that render differently *)
char-data = ' ' | 'p' | '&#x00c9;' | 'X'
content-char-data = ' ' | 'p' | '&#x00c9;' | 'X'

reference     = entity-ref
              | char-ref-dec
              | char-ref-hex
entity-ref    = <'&'> name <';'>
char-ref-dec  = #'&#[0-9]{1,7};'
char-ref-hex  = #'&#x[0-9A-F]{1,6};'


name      = #'[A-Za-z_:][A-Za-z_:\-.0-9]*'

comment-generic = #'<!--(?:[^-]|-[^-])*-->'
comment = #'<!--(?:[A-Za-z0-9_<>/\\ .]|-[A-Za-z0-9_<>/\\ .])*-->'


opt-space = #'\s*'
space = #'\s+'

(*
Definitions for HTML tag attribute values
This is lovingly hand-crafted (i.e. hard-coded) based on data
from MDN: https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes
*)

html-attribute = 'xmlns="' url-generic '"'
               | 'xml:lang="' lang '"'
               | 'prefix="' #'[^"]*' '"'
               | global-attribute
head-attribute = global-attribute
title-attribute = global-attribute
body-attribute = global-attribute

custom-data-attribute = 'data-' name '="' char-data-generic '"'
aria-attribute = 'aria-' name '="' char-data-generic '"'
role-attribute = 'role="' char-data-generic '"'

integer = #"-?[0-9]+"

non-negative-integer = #"[0-9]+"

floating-point-number = #"-?[0-9]+.[0-9]+"

(*
Other hand-crafted (i.e. hard-coded) defintions
*)

lang = #'[a-z][a-z](?:-[A-Za-z][A-Za-z])?'

url-generic = #'[A-Za-z0-9$-_@.&+%=;/#?:]+'

url = 'http://STUB.com/'

mime-type = #'[a-z]+/[a-z0-9-]+'

encoding-label = 'utf-8'

opt-boolean = ''
            | '=""'
            | '="true"'
            | '="false"'
