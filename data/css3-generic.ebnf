(* Basic CSS rules based on: https://www.w3.org/TR/CSS21/grammar.html *)

stylesheet = S ( css-ruleset | css-at-rule | css-comment )* S ;

css-ruleset = css-selector [ ',' S css-selector ]* S '{' S css-assignments S '}' S ;

css-at-rule = prop-@charset
	    | prop-@counter-style
	    | prop-@font-face
	    | prop-@font-feature-values
	    | prop-@import
	    | prop-@keyframes
	    | prop-@media
	    | prop-@namespace
	    | prop-@page
	    | prop-@supports
	    | prop-@viewport

css-selector = css-simple-selector [ css-combinator css-selector | S+ [ css-combinator? css-selector ]? ]? ;

css-simple-selector = css-element-name [ css-hash | css-class | css-pseudo ]*
                    | [ css-hash | css-class | css-pseudo ]+ ;

css-comment = '/*' ( #"[^*]*" | #'[*][^/]' )* '*/' S ;

css-combinator = '+' S
               | '>' S ;

css-element-name = IDENT
                 | '*' ;

css-hash   = '#' NAME ;
css-class  = '.' IDENT ;
css-pseudo = ':' [ IDENT | IDENT '(' S [IDENT S]? ')' ] ;
NAME       = #'[_A-Za-z0-9-]+' ;
IDENT      = #'-?[_A-Za-z][_A-Za-z0-9-]*' ;


